{
    package kql
}

////////////////////////////////////////////////////////
// ast
////////////////////////////////////////////////////////

Root <-
    _ nodes:Nodes _ {
        return root(nodes, c.text, c.pos)
    }

Nodes <-
    head:Element tail:(_ Element _)* {
      return nodes(head, tail)
    }

Element <-
        Groups /
        PropertyRestrictions /
        BooleanOperators /
        FreeTextKeywords

////////////////////////////////////////////////////////
// nesting
////////////////////////////////////////////////////////

Groups <-
    "(" v:Nodes ")" {
        return groupNode("", v, c.text, c.pos)
    } /
    k:Char+ (ColonOperator / EqualOperator) "(" _ v:FreeTextKeywords+ _ ")" {
        return groupNode(k, v, c.text, c.pos)
    }

////////////////////////////////////////////////////////
// property restrictions
////////////////////////////////////////////////////////

PropertyRestrictions <-
    YesNoPropertyRestriction /
    TextPropertyRestriction

YesNoPropertyRestriction <-
    k:Char+ (ColonOperator / EqualOperator) v:("true" / "false"){
        return booleanNode(k, v, c.text, c.pos)
    }

TextPropertyRestriction <-
    k:Char+ (ColonOperator / EqualOperator) v:(String / [^ ()]+){
        return stringNode(k, v, c.text, c.pos)
    }


////////////////////////////////////////////////////////
// free text-keywords
////////////////////////////////////////////////////////

FreeTextKeywords <-
    Phrase /
    Word

Phrase <-
     ColonOperator? _ v:String _ ColonOperator? {
        return stringNode("", v, c.text, c.pos)
    }

Word <-
     ColonOperator? _ v:[^ :()]+ _ ColonOperator? {
        return stringNode("", v, c.text, c.pos)
    }

////////////////////////////////////////////////////////
// operators
////////////////////////////////////////////////////////

BooleanOperators <-
    ("AND" / "OR" / "NOT") {
        return operatorNode(c.text, c.pos)
    }

ColonOperator <-
    ":" {
        return c.text, nil
    }

EqualOperator <-
    "=" {
        return c.text, nil
    }

////////////////////////////////////////////////////////
// misc
////////////////////////////////////////////////////////

Char <-
    [A-Za-z] {
        return c.text, nil
    }

String <-
    '"' v:[^"]* '"' {
        return v, nil
    }

_ <-
    [ \t]*
